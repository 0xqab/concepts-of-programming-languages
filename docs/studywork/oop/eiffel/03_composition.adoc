== Composition

This chapter discusses the object-oriented programming principle of "object composition", where a data type is composed of other data types and is also referred as a "has a" relationship.
This relationship of different data types is explained with the data class "Customer" which includes the data type "Address".

.UML representation of object composition
image::img/composition.svg[]

.Output of example code
----
Max Mustermann, Musterstrasse 1, 12345 Musterstadt
----

=== Go programming language

Composition in Go can be achieved by embedding one struct into another struct with a chosen name.
Beside the "has one" relationship, it is also possible to implement a very simple "has more" relation with a slice which is a self extending implementation of an array.
For more complex scenarios, where a simple slice is not enough, there are lists or maps in the Go libraries.

.address.go (data class)
[source,go,indent=0]
----
package main

import "fmt"

type Address struct {
	street string
	zip string
	city string
}

func (a Address) String() string {
	return fmt.Sprintf("%s, %s %s", a.street, a.zip, a.city)
}
----

.customer.go (composed data class)
[source,go,indent=0]
----
package main

import "fmt"

type Customer struct {
	forename string
	surname string
	address Address <1>
}

func (c Customer) String() string {
	return fmt.Sprintf("%s %s, %s", c.forename, c.surname, c.address) <2>
}
----
<1> Embed data type **Address** as **address** in data type **Customer**
<2> Generate textual representation of **Customer** with the use of the **String()** method of **Address**

.main.go (usage)
[source,go,indent=0]
----
package main

import "fmt"

func main() {
	my_address := Address{"Musterstra√üe 1", "12345", "Musterstadt"}
	my_customer := Customer{"Max", "Mustermann", my_address}
	fmt.Println(my_customer)
}
----

=== Eiffel programming language

//Composition in Go can be achieved by embedding one struct into another struct with a chosen name.
//Beside the "has one" relationship, it is also possible to implement a very simple "has more" relation with a slice which is a self extending implementation of an array.

In the Eiffel programming language, composition is achieved by adding an attribute with the desired data class as the type.
Because all data types in Eiffel are class-based, each new data class with at least one attribute is a compound data type.
To achieve a "has more" relationship, it is necessary to use a list, set, or map implementation which are provided by the rich Eiffel libraries.

.address.e (data class)
[source,eiffel,indent=0]
----
class ADDRESS
inherit
	ANY
		redefine
			out
		end
create
	make
feature {NONE}
	make(a_street, a_zip, a_city: STRING)
		do
			set_street (a_street)
			set_zip (a_zip)
			set_city (a_city)
		end
feature
	street: STRING assign set_street
	zip: STRING assign set_zip
	city: STRING assign set_city
	set_street (a_street: STRING)
		do
			street := a_street
		end
	set_zip (a_zip: STRING)
		do
			zip := a_zip
		end
	set_city (a_city: STRING)
		do
			city := a_city
		end
	out: STRING
		do
			Result := street.out + ", " + zip.out + " " + city.out
		end
end
----

.customer.e (composed data class)
[source,eiffel,indent=0]
----
class CUSTOMER
inherit
	ANY
		redefine
			out
		end
create
	make
feature {NONE}
	make (a_forename, a_surname: STRING; a_address: ADDRESS)
		do
			set_forename (a_forename)
			set_surname (a_surname)
			set_address (a_address)
		end
feature
	forename: STRING assign set_forename
	surname: STRING assign set_surname
	address: ADDRESS assign set_address <1>
	set_forename (a_forename: STRING)
		do
			forename := a_forename
		end
	set_surname (a_surname: STRING)
		do
			surname := a_surname
		end
	set_address (a_address: ADDRESS)
		do
			address := a_address
		end
	out: STRING
		do
			Result := forename.out + " " + surname.out + ", " + address.out <2>
		end
end
----
<1> Use data type **ADDRESS** as attribute **address** in data type **CUSTOMER**
<2> Generate textual representation of **CUSTOMER** with the use of the **out** function of **ADDRESS**

.application.e (usage)
[source,eiffel,indent=0]
----
class
	APPLICATION
create
	make
feature {NONE}
	make
		local
			my_customer: CUSTOMER
			my_address: ADDRESS
		do
			create my_address.make ("Musterstrasse 1", "12345", "Musterstadt")
			create my_customer.make ("Max", "Mustermann", my_address)
			print (my_customer.out + "%N")
		end
end
----
